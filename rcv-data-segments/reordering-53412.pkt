//
// Copyright (c) 2018 Michael Tuexen
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions
// are met:
// 1. Redistributions of source code must retain the above copyright
//    notice, this list of conditions and the following disclaimer.
// 2. Redistributions in binary form must reproduce the above copyright
//    notice, this list of conditions and the following disclaimer in the
//    documentation and/or other materials provided with the distribution.
//
// THIS SOFTWARE IS PROVIDED BY THE AUTHOR AND CONTRIBUTORS ``AS IS'' AND
// ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
// IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
// ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR OR CONTRIBUTORS BE LIABLE
// FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
// DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
// OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
// HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
// LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
// OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
// SUCH DAMAGE.
//

--ip_version=ipv4
--tolerance_usecs=10000

// Ensure that all relevant sysctl variables have their default variables.
 0.00 `sysctl -w net.inet.tcp.sack.enable=1`
// Flush host cache.
+0.00 `sysctl -w net.inet.tcp.hostcache.purgenow=1`
// Create a listening TCP socket
+0.00 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
+0.00 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
+0.00 bind(3, ..., ...) = 0
+0.00 listen(3, 1) = 0
// Establish the connection
+0.00 < S   0:0(0) win 65535 <mss 1460,sackOK,eol,eol>
+0.00 > S.  0:0(0) ack 1 win 65535 <mss 1460,sackOK,eol,eol>
+0.01 <  .  1:1(0) ack 1 win 65535
+0.00 accept(3, ..., ...) = 4
+0.00 close(3) = 0
// Data transfer
+0.00 <  . 16:32(16) ack  1 win 65535 // Fifth segment
+0.00 >  .  1:1(0)   ack  1 win 65535 <nop,nop,sack 16:32>
+0.00 <  .  4:8(4)   ack  1 win 65535 // Third segment
+0.00 >  .  1:1(0)   ack  1 win 65535 <nop,nop,sack 4:8 16:32>
+0.00 <  .  8:16(8)  ack  1 win 65535 // Fourth segment
+0.00 >  .  1:1(0)   ack  1 win 65535 <nop,nop,sack 4:32>
+0.00 <  .  1:2(1)   ack  1 win 65535 // First segment
+0.00 >  .  1:1(0)   ack  2 win 65535 <nop,nop,sack 4:32>
+0.00 recv(4, ..., 1024, MSG_PEEK) = 1
+0.00 <  .  2:4(2)   ack  1 win 65535 // Second segment
+0.00 >  .  1:1(0)   ack 32 win 65535
+0.00 recv(4, ..., 1024, MSG_PEEK) = 31
// Teardown the connection
+0.00 recv(4, ..., 1024, 0) = 31
+0.10 < F. 32:32(0)  ack  1 win 65535
+0.00 >  .  1:1(0)   ack 33 win 65535
+0.00 recv(4, ..., 1024, 0) = 0 
+0.00 close(4) = 0
+0.00 > F.  1:1(0)   ack 33 win 65535
+0.01 <  . 33:33(0)  ack  2 win 65535
